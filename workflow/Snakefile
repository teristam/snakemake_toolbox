from glob import glob

rule all:
    input:
        expand('{session}/processed/workflow.done', session=glob('data/*'))

rule combine:
    input:
        A='{session}/A.txt',
        B='{session}/B.txt'
    message:
        '--Combining files--'
    output:
        combined='{session}/processed/combined.txt'
    log:
        '{session}/log/combine.log'
    script:
        'scripts/combine.py'

rule capitalize:
    input:
        combined = rules.combine.output.combined
    message:
        '--- Running capitalize---'
    output:
        capitalized = '{session}/processed/capitalized.txt'
    log:
        notebook = '{session}/log/capitalize.ipynb'
    conda:
        'env/notebook.yml'
    notebook:
        'notebooks/capitalize.ipynb'

rule plot:
    input:
        capitalized ='{session}/A.txt'
    message:
        '--- Plotting figures ---'
    log:
        '{session}/log/plot.log'        
    output:
        figures_dir = directory('{session}/processed/figures/'),
    script:
        'scripts/plot.R'

rule final:
    input:
        rules.plot.output,
        rules.capitalize.output
    
    log:
        '{session}/log/final.log'
    output:
        touch('{session}/processed/workflow.done'),
        '{session}/log/code_backup.tar.gz'
    shell:
        '''
        # Create a temporary directory for the clean copy
        TMPDIR=$(mktemp -d)

        # Copy working directory to temp, excluding .git and other hidden files
        rsync -a --exclude='.*' --exclude='data/' ./ "$TMPDIR/" 

        # Create the tarball
        tar -czf "{wildcards.session}/log/code_backup.tar.gz" -C "$TMPDIR" . 

        # Clean up
        rm -rf "$TMPDIR"
        '''